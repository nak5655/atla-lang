using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Reflection;

using Atla.Lang;
using Atla.Lang.Hir;
using Atla.Lang.Mir;
using Atla.Lang.Parse;

// K正規化
// TODO Mir.from に変更(HIRがMIRの詳細に依存すべきではない)
namespace Atla.Lang.Hir
{
    [Record]
    public class KNormal {
        public proc: IEnumerable[Mir.Ins];
        public value: option[Mir.Value]; // voidのときnone
    }
    
    variant Callable {
        | MethodInfo { methodInfo: System.Reflection.MethodInfo }
        | Inline { proc: Proc }
        | Constructor { constructorInfo: System.Reflection.ConstructorInfo }
        | InstanceMethod { proc: IEnumerable[Mir.Ins]; instance: Mir.Value; methodInfo: System.Reflection.MethodInfo }
    }
    
    // TODO Symbolがすでに一意に決まっていればHir.Scopeいらない?
    public class Trans
    {
        getCallable(frame: Frame, scope: Hir.Scope, hir: Hir.Expr): Result[Callable, Problem] {
            match (hir) {
                | Hir.Expr.Id as id => {
                    def ty = hir.type.pruned();
                    match (id.getSymbol(scope)) {
                        | Some(sym) => match (sym.kind) {
                            | SymbolKind.Method(mi) => Result.Success(Callable.MethodInfo(mi))
                            | SymbolKind.InlineMethod(proc) => Result.Success(Callable.Inline(proc))
                            | SymbolKind.Constructor(ci) => Result.Success(Callable.Constructor(ci))
                            | _ => match (ty) {
                                | Atla.Lang.Type.Fn => Result.Success(Callable.MethodInfo(ty.ToSystemType().GetMethod("Invoke"))) // System.Func.Invoke や System.Action.Invoke など
                                | _ => Result.Failure(Problem(Severity.Error, $"$hir is not a function.", Span.zero()))
                            }
                        }
                        | _ => Result.Failure(Problem(Severity.Error, $"$hir is not a function.", Span.zero()))
                    }
                }
                | Hir.Expr.Member(expr, name) => {
                    trans(frame, scope, expr).bind(instKn => match (instKn) {
                        | KNormal(proc, Some(inst)) => {
                            def instTy = expr.type.pruned();
                            def (_, m) = instTy.findMember(name, hir.type);
                            
                            match (m) {
                                | Some(Either.Right(MethodInfo as mi)) => {
                                    Result.Success(Callable.InstanceMethod(proc, inst, mi))
                                }
                                | _ => Result.Failure(Problem(Severity.Error, $"$instTy has no members.", expr.span))
                            }
                        }
                        | _ => Result.Failure(Problem(Severity.Error, $"$expr has no value.", expr.span))
                    })
                }
                | Hir.Expr.StaticMember(clsName, name) => {
                    mutable ty = scope.resolveType(clsName);
                    match (ty.pruned()) {
                        | Type.Native as nt => {
                            def mi = nt.type.GetMethods().Find(methodInfo => {
                                if (methodInfo.Name == name) {
                                    match (hir.type.pruned()) {
                                        | it is Type.Fn => it.canUnify(methodInfo.toFnType(scope))
                                        | _ => true
                                    }
                                } else {
                                    false
                                }
                            });
                            if (mi.IsSome) {
                                Result.Success(Callable.MethodInfo(mi.Value))
                            } else {
                                Result.Failure(Problem(Severity.Error, "$hir does not exist.", hir.span))
                            }
                        }
                        // TODO
                    }
                }
            }
        }
        
        declareId(frame: Frame, scope: Hir.Scope, id: Hir.Expr.Id): Result[Symbol, Problem] {
            match (id.getSymbol(scope)) {
                | Some(sym) => {
                    if (sym.isLocal()) {
                        frame.declareLocal(sym);
                            
                        Result.Success(sym)
                    } else {
                        Result.Failure(Problem(Severity.Error, $"$(id.name) is not declared as local variable.", id.span))
                    }
                }
                | _ => Result.Failure(Problem(Severity.Error, $"$(id.name) does not exist in scope.", id.span))
            }
        }
        
        public trans(frame: Frame, scope: Hir.Scope, hir: Hir.Stmt): Result[IEnumerable[Mir.Ins], Problem] {
            match (hir) {
                | Hir.Stmt.Expr(e) => trans(frame, scope, e).bind(a: KNormal => match (a) {
                    | KNormal(proc, _) => Result.Success(proc)
                    | _ => Result.Failure(Problem(Severity.Error, $"Could not interpret $e as a value.", hir.span))
                })
                | Hir.Stmt.Var(id, expr, _) => {
                    declareId(frame, scope, id).bind(sym => {
                        def re = trans(frame, scope, expr);
                        re.bind(kn => match (kn) {
                            | KNormal(proc, Some(res)) => Result.Success(proc.Concat([Mir.Ins.Assign(sym, res)]))
                            | _ => Result.Failure(Problem(Severity.Error, $"$expr has no value.", expr.span))
                        })
                    })
                }
                | Hir.Stmt.Assign(id, expr) => {
                    match (id.getSymbol(scope)) {
                        | Some(sym) => {
                            def re = trans(frame, scope, expr);
                            re.bind(kn => match (kn) {
                                | KNormal(proc, Some(res)) => Result.Success(proc.Concat([Mir.Ins.Assign(sym, res)]))
                                | _ => Result.Failure(Problem(Severity.Error, $"$expr has no value.", expr.span))
                            })
                        }
                        | _ => Result.Failure(Problem(Severity.Error, $"$(id.name) does not exist in scope.", id.span))
                    }
                }
                | Hir.Stmt.Return(e) => {
                    match (scope) {
                        | it is Hir.Scope.Block => trans(frame, scope, e).bind(a: KNormal => match (a) {
                            // ブロックスコープならReturnじゃなくてJumpする
                            | KNormal(proc, Some(res)) => Result.Success(proc.Concat([Mir.Ins.Assign(it.retSymbol, res), Mir.Ins.Jump(it.endLabel)]))
                            | KNormal(proc, _) => Result.Success(proc.Concat([Mir.Ins.Jump(it.endLabel)]))
                            | _ => Result.Failure(Problem(Severity.Error, $"Could not interpret $e as a value.", e.span))
                        })
                        | Hir.Scope.Fn => trans(frame, scope, e).bind(a: KNormal => match (a) {
                            | KNormal(proc, Some(res)) => Result.Success(proc.Concat([Mir.Ins.RetValue(res)]))
                            | KNormal(proc, _) => Result.Success(proc.Concat([Mir.Ins.Ret()]))
                            | _ => Result.Failure(Problem(Severity.Error, $"Could not interpret $e as a value.", e.span))
                        })
                        | _ => Result.Failure(Problem(Severity.Error, $"Could not find the return destination. $hir", hir.span))
                    }
                }
                | Hir.Stmt.For as st => {
                    def rIter = trans(frame, scope, st.iter);
                    
                    declareId(frame, scope, st.id).bind(_ => {
                        // Iteratorの型を確認
                        match (st.iter.type.pruned()) {
                            | Type.Generics(Type.Native as it, [itemType]) when it.type.getInterfaceHierarchy().Contains(Type.Iterator.type) => {
                                rIter.bind(nIter => st.body.Map(s => trans(frame, st.bodyScope, s)).seq().bind(body => {
                                    match (nIter.value) {
                                        | Some(iter) => {
                                            def begin = Mir.Label();
                                            def end = Mir.Label();
                                        
                                            def hasNext = frame.declareTemp(itemType);
                                            def cond = [
                                                Mir.Ins.CallAssign(hasNext, Type.Iterator.type.GetMethod("MoveNext"), [iter]),
                                                Mir.Ins.JumpFalse(Mir.Value.Sym(hasNext), end)
                                            ];
                                            // Iteratorの初期化 -> ループ先頭ラベル -> ループ脱出判断 -> 本体 -> ループ先頭へジャンプ -> ループ終了ラベル
                                            Result.Success(nIter.proc.Concat(Mir.Ins.MarkLabel(begin) :: cond).Concat(body.flatten()).Concat([
                                                Mir.Ins.Jump(begin), Mir.Ins.MarkLabel(end)
                                            ]))
                                        }
                                        | _ => Result.Failure(Problem(Severity.Error, "Failed to translate an iterator as a value.", hir.span))
                                    }
                                }))
                            }
                            | t => Result.Failure(Problem(Severity.Error, $"Expected an iterator, but got $t.", hir.span))
                        }
                    })
                }
            }
        }
        
        public trans(frame: Frame, scope: Hir.Scope, hir: Hir.Expr): Result[KNormal, Problem] {
            match (hir) {
                // TODO
                | Hir.Expr.Unit => Result.Success(KNormal([], None())) // TODO Unit Singleton
                | Hir.Expr.Bool(value) => Result.Success(KNormal([], Some(Mir.Value.Imm(Mir.Imm.Bool(value)))))
                | Hir.Expr.Int(value) => Result.Success(KNormal([], Some(Mir.Value.Imm(Mir.Imm.Int(value)))))
                | Hir.Expr.Double(value) => Result.Success(KNormal([], Some(Mir.Value.Imm(Mir.Imm.Double(value)))))
                | Hir.Expr.String(s) => Result.Success(KNormal([], Some(Mir.Value.Imm(Mir.Imm.String(s)))))
                | Hir.Expr.Id(name, _) as id => {
                    match (id.getSymbol(scope)) {
                        | Some(sym) => match (sym.kind) {
                            | SymbolKind.Arg =>  match (NCollectionsExtensions.Find(frame.args, arg => arg.name == sym.name)) { // 同じ名前の引数はないので名前が一致すればいい
                                | Some(arg) => Result.Success(KNormal([], Some(Mir.Value.Sym(arg))))
                                | None => Result.Failure(Problem(Severity.Error, $"Could not find $name in arguments.", hir.span))
                            }
                            | SymbolKind.Local => match (NCollectionsExtensions.Find(frame.locs, loc => loc.name == sym.name && loc.type.canUnify(sym.type))) { // TODO 同値比較でいい?(symをそのままつかえばいい?)
                                | Some(loc) => Result.Success(KNormal([], Some(Mir.Value.Sym(loc))))
                                | None => Result.Failure(Problem(Severity.Error, $"Could not find $name in locals.", hir.span))
                            }
                            | SymbolKind.Field as fi => {
                                def insts = scope.resolveVar("this", Type.Unknown()); // TODO 型情報がないので不安
                                if (insts.Count() > 0) {
                                    def thisSymbol = insts.First();
                                    Result.Success(KNormal([], Some(Mir.Value.Field(thisSymbol, fi.fieldInfo))))
                                } else {
                                    Result.Failure(Problem(Severity.Error, "Could not find an instance named \"this\" in locals.", hir.span))
                                }
                            }
                        }
                        | _ => Result.Failure(Problem(Severity.Error, $"$name is not defined as $(id.type) type.", hir.span))
                    }
                }
                | Hir.Expr.Member(expr, name) => {
                    match (trans(frame, scope, expr)) {
                        | Result.Success(KNormal(proc, Some(val))) => {
                            def (ty, mfimi) = expr.type.findMember(name, hir.type);
                            
                            match (mfimi) {
                                | Some(Either.Left(fi)) => {
                                    def sym = frame.declareTemp(ty);
                                    if (fi != null)
                                        Result.Success(KNormal(proc.Concat([Mir.Ins.Assign(sym, val)]), Some(Mir.Value.Field(sym, fi))))
                                    else
                                        Result.Failure(Problem(Severity.Error, $"Could not find field $name in $ty.", hir.span))
                                }
                                | Some(Either.Right(mi)) => {
                                    def sym = frame.declareTemp(Type.fromSystemType(mi.ReturnType));
                                    Result.Success(KNormal(proc.Concat([Mir.Ins.CallAssign(sym, mi, [val])]), Some(Mir.Value.Sym(sym))))
                                }
                                | _ => Result.Failure(Problem(Severity.Error, $"Could not find field $name in $ty.", hir.span))
                            }
                        }
                        | Result.Success(_) => Result.Failure(Problem(Severity.Error, $"Could not interpret $expr as a value.", hir.span))
                        | Result.Failure(e) => Result.Failure(e)
                    }
                }
                | Hir.Expr.Apply(fn, args) => {
                    // 引数の評価順に注意
                    // K正規化
                    def kArgRs = args.Map(arg => trans(frame, scope, arg));
                    foreach (kArgR in kArgRs) {
                        match (kArgR) {
                            | Result.Failure(e) => return Result.Failure(e)
                            | _ => ()
                        }
                    }
                    def kArgs = kArgRs.Map(a => a.getResult().Value);
                    
                    // 引数の準備
                    mutable procArgs = kArgs.SelectMany(_.proc);
                    mutable resArgs = kArgs.Map(_.value).Filter(_.IsSome).Map(_.Value);
                            
                    match (getCallable(frame, scope, fn)) {
                        | Result.Success(Callable.MethodInfo(m)) => {
                            // 引数がvoidのみの時
                            when (args.Length == 1 && args.First().type.isVoid()) {
                                procArgs = [];
                                resArgs = [];
                            }
                            if (m.ReturnType == typeof(void)) {
                                // 返り値がvoidのとき
                                Result.Success(KNormal(procArgs.Concat([Mir.Ins.Call(Either.Left(m), resArgs)]), None()))
                            } else {
                                def tmp = frame.declareTemp(m.ReturnType);
                            
                                Result.Success(KNormal(procArgs.Concat([Mir.Ins.CallAssign(tmp, m, resArgs)]), Some(Mir.Value.Sym(tmp))))
                            }
                        }
                        | Result.Success(Callable.InstanceMethod(proc, inst, mi)) => {
                            // 引数がvoidのみの時
                            when (args.Length == 1 && args.First().type.isVoid()) {
                                procArgs = [];
                                resArgs = [];
                            }
                            if (mi.ReturnType == typeof(void)) {
                                // 返り値がvoidのとき
                                Result.Success(KNormal(procArgs.Concat(proc).Concat([Mir.Ins.Call(Either.Left(mi), inst :: resArgs)]), None()))
                            } else {
                                def tmp = frame.declareTemp(mi.ReturnType);
                            
                                Result.Success(KNormal(procArgs.Concat(proc).Concat([Mir.Ins.CallAssign(tmp, mi, inst :: resArgs)]), Some(Mir.Value.Sym(tmp))))
                            }
                        }
                        | Result.Success(Callable.Inline(proc)) => {
                            match (fn.type.pruned()) {
                                | Atla.Lang.Type.Fn(_, ret) => {
                                    def tmp = frame.declareTemp(ret);
                                    // 引数の数を確認して呼び出し命令生成
                                    def body = match (proc) {
                                        | Proc.DstArg2(body) => body(tmp, resArgs.Nth(0), resArgs.Nth(1))
                                    };
                                    Result.Success(KNormal(procArgs.Concat(body), Some(Mir.Value.Sym(tmp))))
                                }
                                | _ => Result.Failure(Problem(Severity.Error, $"$fn must be a function.", fn.span))
                            }
                        }
                        | Result.Success(Callable.Constructor(ci)) => {
                            match (fn.type.pruned()) {
                                | Atla.Lang.Type.Fn(_, ret) => {
                                    def tmp = frame.declareTemp(ret);
                            
                                    Result.Success(KNormal(procArgs.Concat([Mir.Ins.New(tmp, ci, resArgs)]), Some(Mir.Value.Sym(tmp))))
                                }
                                | _ => Result.Failure(Problem(Severity.Error, $"$fn must be a function.", fn.span))
                            }
                        }
                        | Result.Failure(e) => Result.Failure(e)
                    }
                }
                | Hir.Expr.Block(blockScope, ss) => {
                    // TODO Block.stmtsをlist[Stmt]にする
                    
                    // TODO 返り値がvoidの時
                    
                    // ブロックの帰り値を確保
                    when (!blockScope.retType.isVoid())
                        blockScope.retSymbol = frame.declareTemp(blockScope.retType);
                    
                    def stmts = ss.ToNList();
                    def body = if (stmts.Length > 0) {
                        match (stmts.Last()) {
                            | Hir.Stmt.Expr(e) => {
                                // 最後が式文のとき
                                if (stmts.Length > 1) {
                                    // 最後の式以外を変換
                                    mutable res = trans(frame, blockScope, stmts.Head);
                                    foreach (s in stmts.Tail.Take(stmts.Length - 2)) {
                                        res = res.bind(xs => trans(frame, blockScope, s).map(ys => xs.Concat(ys)));
                                    }
                                    // 最後の式の評価結果をblockの帰り値に代入
                                    def rc = trans(frame, blockScope, e);
                                    res.bind(xs => rc.bind(c => {
                                        match (c) {
                                            | KNormal(proc, Some(res)) when !blockScope.retType.isVoid() => Result.Success(xs.Concat(proc).Concat([Mir.Ins.Assign(blockScope.retSymbol, res)]))
                                            | KNormal(proc, _) => Result.Success(xs.Concat(proc))
                                        }
                                    }))
                                } else {
                                    // 式の評価結果をblockの帰り値に代入
                                    trans(frame, blockScope, e).bind(xs => {
                                        match (xs) {
                                            | KNormal(proc, Some(res)) when !blockScope.retType.isVoid() => Result.Success(proc.Concat([Mir.Ins.Assign(blockScope.retSymbol, res)]))
                                            | KNormal(proc, _) => Result.Success(proc)
                                        }
                                    })
                                }
                            }
                            | _ => {
                                stmts.Tail.Fold(trans(frame, blockScope, stmts.Head), (v, acc) => acc.bind(xs => trans(frame, blockScope, v).map(ys => xs.Concat(ys))))
                            }
                        }
                    } else {
                        Result.Success([]) // TODO unitを返す
                    }
                    
                    if (blockScope.retType.isVoid()) {
                        body.bind(b => Result.Success(KNormal(b, None())))
                    } else {
                        body.bind(b => Result.Success(KNormal(b, Some(Mir.Value.Sym(blockScope.retSymbol)))))
                    }
                }
                | Hir.Expr.Fn => throw Exception("Internal Error") // Flatで取り除かれてるはず
                | Hir.Expr.Switch(ents) => {
                    mutable racc = Result.Success([]);
                    def endLabel = Mir.Label(); // switch式を抜けるラベル
                    def tmp = frame.declareTemp(hir.type.pruned().ToSystemType()); // switch式の結果
                    
                    def bodies = ents.Map(ent => (Mir.Label(), trans(frame, scope, ent.body)));
                    
                    // 分岐命令
                    foreach (i in Enumerable.Range(0, ents.Count())) {
                        def ent = ents.Nth(i);
                        def (label, _) = bodies.Nth(i);
                        def mne = trans(frame, scope, ent.pred);
                        racc = racc.bind(acc => mne.bind(ne => match (ne) {
                            // 条件式がTrueならbodyにジャンプ
                            | KNormal(proc, Some(res)) => Result.Success(acc.Concat(proc).Concat([
                                Mir.Ins.JumpTrue(res, label)
                            ]))
                            | _ => Result.Failure(Problem(Severity.Error, $"Switch Predicate $ent.pred is not a Value.", ent.span))
                        }));
                    }
                    
                    // 分岐先
                    foreach (i in Enumerable.Range(0, ents.Count())) {
                        def ent = ents.Nth(i);
                        def (label, rbody) = bodies.Nth(i);
                        racc = racc.bind(acc => rbody.bind(body => match (body) {
                            | KNormal(proc, Some(res)) => {
                                // 分岐ラベルに続けてボディを追加
                                Result.Success(acc.Concat([Mir.Ins.MarkLabel(label)]).Concat(proc).Concat([
                                    Mir.Ins.Assign(tmp, res),
                                    Mir.Ins.Jump(endLabel), // 分岐の終わりまでジャンプ
                                ]))
                            }
                            | _ => Result.Failure(Problem(Severity.Error, $"Switch Entry $ent should return a Value.", ent.span))
                        }));
                    }
                    
                    racc.map(r => KNormal(r.Concat([Mir.Ins.MarkLabel(endLabel)]), Some(Mir.Value.Sym(tmp))))
                }
            }
        }
         
        public trans(import: Hir.Import): void {
            // TODO
            //| Hir.Decl.Import(name) => {
            //    def type = System.Type.GetType(name);
            //    typeTable.Add(name, Hir.Type.Native(type));
            //    // TODO: ドット区切り対応 System.Console を Console として追加登録する
            //    []
            //}
        }
        
        public implType(mir: Mir.Type, tyDef: Atla.Lang.Type.Define): option[Problem] {
            // コンストラクタ
            def ctor = addConstructor(tyDef.scope.parent, mir, tyDef); // 型が宣言されているスコープと同じスコープで宣言
            
            // 先にメソッドを定義
            def methods: List[Hir.Method * Mir.Method] = List();
            foreach (method in tyDef.methods) {
                match (addMethod(mir, tyDef.scope, method)) {
                    | Result.Failure(e) => return Some(e)
                    | Result.Success(m) => {
                        methods.Add(method, m);
                        method.info = Some(m.builder);
                    }
                }
            }
            
            // フィールドを定義
            def fields: List[TypeField * Mir.Field] = List();
            foreach (field in tyDef.fields) {
                def mf = mir.defineField(field.name, field.type.ToSystemType());
                field.info = mf.builder; // FieldInfoを共有
                fields.Add(field, mf);
                match (field.getSymbol()) {
                    | Some(sym) => sym.kind = SymbolKind.Field(mf.builder);
                    | _ => return Some(Problem(Severity.Error, $"$field does not have a symbol.", Span.zero()));
                }
            }
            
            // フィールド(FieldInfo)が定義されてからコンストラクタの実装を追加
            implConstructor(tyDef, ctor);
            
            // メソッドを実装
            foreach ((hm, mm) in methods) {
                match (implMethod(hm, mm)) {
                    | Some(e) => return Some(e)
                    | _ => {}
                }
            }
            
            // TODO フィールドの初期化
            
            None()
        }
        
        public addType(scope: Hir.Scope, mod: Mir.Module, tyDef: Atla.Lang.Type.Define): Mir.Type {
            def ty = Mir.Type(mod, tyDef.name);
            mod.types.Add(ty);
            
            tyDef.info = Some(ty.builder); // System.Typeの代わりになる(ToSystemTypeが使えるようになる)
            
            ty
        }
        
        public addConstructor(scope: Hir.Scope, ty: Mir.Type, tyDef: Atla.Lang.Type.Define): Mir.Constructor {
            def ctor = ty.defineConstructor(tyDef.fields.Map(fi => fi.type.ToSystemType()).ToArray());
            
            // コンストラクタのSymbolKindを更新
            def syms = scope.resolveVar(tyDef.name, Type.Unknown());
            foreach (sym in syms) {
                match (sym.type.pruned()) {
                    | it is Type.Fn when it.ret == tyDef => sym.kind = SymbolKind.Constructor(ctor.builder);
                    | _ => {}
                }
            }
            
            ctor
        }
        
        public implConstructor(tyDef: Atla.Lang.Type.Define, mir: Mir.Constructor): void {
            def inst = Symbol("this", tyDef, SymbolKind.Arg(), tyDef.scope.depth + 1);
            mir.frame.declareArg(inst);
            
            def proc = List();
            
            // インスタンス初期化(System.Object決め打ち)
            def objCtor = typeof(object).GetConstructor(Array.Empty());
            proc.AddRange([Mir.Ins.Call(Either.Right(objCtor), [Mir.Value.Sym(mir.frame.args.First())])]);
            
            foreach (field in tyDef.fields) {
                def arg = Symbol(field.name, field.type, SymbolKind.Arg(), tyDef.scope.depth + 1);
                
                // フィールドに渡される引数を追加
                mir.frame.declareArg(arg);
                
                // 代入命令を追加
                proc.AddRange([Mir.Ins.AssignField(inst, field.info, Mir.Value.Sym(arg))]);
            }
            
            proc.Add(Mir.Ins.Ret());
            
            mir.body = proc.ToNList();
        }
        
        public addMethod(mc: Mir.MethodContainer, scope: Hir.Scope, hir: Hir.Method): Result[Mir.Method, Problem] {
            def type = hir.fn.type.pruned();
            match (type) {
                | Type.Fn => ()
                | _ => return Result.Failure(Problem(Severity.Error, $"$hir is $type, it should be a function.", Span.zero()))
            }
            def fnType = type :> Type.Fn;
            
            def method = mc.defineMethod(hir.name, fnType.args.Map(_.ToSystemType()), fnType.ret.ToSystemType());
            foreach (i in Enumerable.Range(0, fnType.args.Length)) {
                match (hir.fn.scope.resolveArgNth(i)) {
                    | Some(sym) when sym.name == hir.fn.args.Nth(i).name => method.frame.declareArg(sym) // 一応名前チェックする
                    | _ => return Result.Failure(Problem(Severity.Error, $"Failed to create method frame. Could not find the argument $(hir.fn.args.Nth(i).name) in function scope.", Span.zero()))
                }
            }
            
            // Bodyを変換する前にSymbolにMethodInfoを設定する（再帰関数に対応できる）
            def syms = scope.resolveVar(hir.name, fnType);
            if (syms.Count() == 1) {
                syms.First().kind = SymbolKind.Method(method.builder);
            } else {
                return Result.Failure(Problem(Severity.Error, $"Could not resolve method $(hir.name) as $fnType.", Span.zero()))
            }
            
            Result.Success(method)
        }
        
        public implMethod(hir: Hir.Method, mir: Mir.Method): option[Problem] {
            match (trans(mir.frame, hir.fn.scope, hir.fn.body)) {
                | Result.Success(KNormal(proc, res)) => mir.body = match (res) {
                    | Some(r) => proc.Concat([Mir.Ins.RetValue(r)]).ToList().ToNList();
                    | _ => proc.Concat([Mir.Ins.Ret()]).ToList().ToNList()
                }
                | Result.Success(_) => return Some(Problem(Severity.Error, $"Could not interpret $(hir.fn.body) as a value.", Span.zero()))
                | Result.Failure(e) => return Some(e);
            }
            
            None()
        }
        
        public generateModule(asm: Mir.Assembly, hir: Hir.Module): Result[Mir.Module, Problem] {
            def mod = Mir.Module(asm, hir.name);
            
            // 先に宣言だけして、そのあと関数本体を変換する
            foreach ((ht, mt) in hir.types.Map(t => (t, addType(hir.scope, mod, t)))) {
                match (implType(mt, ht)) {
                    | Some(e) => return Result.Failure(e);
                    | _ => {}
                }
            }
            
            // 先に宣言だけして、そのあと関数本体を変換する
            foreach ((m, r) in hir.methods.Map(m => (m, addMethod(mod, hir.scope, m)))) {
                match (r) {
                    | Result.Failure(e) => return Result.Failure(e);
                    | Result.Success(method) => {
                        match (implMethod(m, method)) {
                            | Some(e) => return Result.Failure(e);
                            | _ => {}
                        }
                    }
                }
            }
          
            Result.Success(mod)
        }
        
        public trans(asm: Hir.Assembly): Result[Mir.Assembly, Problem] {
            def mir = Mir.Assembly(asm.name, asm.fileName);
            foreach ((name,mod) in asm.modules.KeyValuePairs) {
                match (generateModule(mir, mod)) {
                    | Result.Success(m) => mir.modules[name] = m
                    | Result.Failure(e) => return Result.Failure(e)
                }
                
            }
            Result.Success(mir)
        }
    }
    
    public module HirTransExtensions {
        public findMember(this type: Type, name: string, expected: Type): Type * option[Either[FieldInfo, MethodInfo]] {
            match (type.pruned()) {
                | Type.Define as ty => {
                    // 定義中の型
                    def fis = ty.fields.Filter(fi => fi.name == name);
                    if (fis.Count() == 0) {
                        def fnType = match (expected.pruned()) {
                            | Type.Fn(args, ret) => Type.Fn(ty :: args, ret) // インスタンスメソッド
                            | _ => Type.Fn([ty], expected); // 自分自身のみを引数とするインスタンスメソッド
                        };
                        def mis = ty.methods.Filter(mi => mi.name == name && mi.fn.type.canUnify(fnType));
                        if (mis.Count() == 0)
                            (ty, None())
                        else
                            (ty, Some(Either.Right(mis.First().info.Value)))
                    } else
                        (ty, Some(Either.Left(fis.First().info)))
                }
                | Type.Native as ty => {
                    // 定義済みの型
                    def fi = ty.type.GetField(name);
                    if (fi == null) {
                        mutable methods = ty.type.GetMethods().Where(m => m.Name == name);
                        match (expected.pruned()) {
                            | Type.Fn as fnType => {
                                // 期待される関数の型で判別
                                def args = fnType.args.ToList();
                                when (args.Count() > 0 && args.First().canUnify(ty)) { // インスタンス関数なら自分の型が先頭にあるはずなのでそれを除く
                                    args.RemoveAt(0)
                                }
                                when (args.Count() == 1 && args.First().isVoid()) { // unitが唯一の引数ならそれを除く
                                    args.RemoveAt(0)
                                }
                                // ここで、インスタンス関数やUnitのみ引数にとる関数は引数なしとなる
                                
                                methods = methods.Where(m => {
                                    def ps = m.GetParameters().NToList();
                                    ps.Count() == args.Count() && ps.Zip(args.NToList()).All((p, arg) => {
                                        // 期待される引数の型が実際の関数の型に受け入れられるか確認
                                        if (p.ParameterType.IsInterface)
                                            arg.ToSystemType().getInterfaceHierarchy().Contains(p.ParameterType)
                                        else
                                            arg.ToSystemType().getClassHierarchy().Contains(p.ParameterType)
                                    })
                                });
                                
                                if (methods.Count() == 1)
                                    (ty, Some(Either.Right(methods.First())))
                                else
                                    (ty, None())
                            }
                            | _ => {
                                if (methods.Count() == 1)
                                    (ty, Some(Either.Right(methods.First())))
                                else
                                    (ty, None())
                            }
                        }
                    } else
                        (ty, Some(Either.Left(fi)))
                }
                | Type.Generics(b, _) => {
                    b.findMember(name, expected)
                }
            }
        }
    }
}
