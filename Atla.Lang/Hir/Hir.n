using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Reflection;

using Atla.Lang;
using Atla.Lang.Parse;


/**
脱糖済み
型推論の対象
第一級関数の展開
**/
namespace Atla.Lang.Hir
{
    public interface HasExpr {
        // Bottom-Upで再帰的に出現する式を置換する 平滑化(Flat)で使う ほかにも使えるかも
        replaceExpr(scope: Hir.Scope, f: Hir.Scope * Hir.Expr -> Hir.Expr): void;
    }
    
    public variant Error {
        | IllegalIndentation { message: string }
        | DuplicatedDeclaration { name: string; type: Atla.Lang.Type; already: Atla.Lang.Type }
        | UndefinedReference {
            name: string; type: Atla.Lang.Type;
        }
        | Type {
            value: TypeError;
            
            public override Equals(other: object): bool {
                match (other) {
                    | Error.Type(te) => value.Equals(te)
                    | _ => false
                }
            }
            
            public override GetHashCode(): int {
                value.GetHashCode()
            }
        }
        | Syntax { message: string }
        
        override public ToString(): string {
            match (this) {
                | IllegalIndentation as e => $"IllegalIndentation: $(e.message)"
                | DuplicatedDeclaration as e => $"DuplicatedDeclaration: Cannot declare $(e.name) as $(e.type), it was already declared as $(e.already)."
                | UndefinedReference as e => $"UndefinedReference: $(e.name) is not declared as $(e.type)."
                | Type as e => $"TypeError: $(e.value)"
            }
        }
    }
    
    public variant Pattern: HasSpan[Pattern] {
        | Any
        | Int { value: int }
        
        public scanErrors(): IEnumerable[(Error * Span)] {
            [] // TODO
        }
    }
    
    public variant Stmt: HasSpan[Stmt], HasExpr {
        | Expr { mutable expr: Hir.Expr }
        | Var { mutable id: Hir.Expr.Id; mutable expr: Hir.Expr; isMut: bool; }
        | Assign { mutable id: Hir.Expr.Id; mutable expr: Hir.Expr }
        | Return { mutable expr: Hir.Expr }
        
        public replaceExpr(scope: Hir.Scope, f: Hir.Scope * Hir.Expr -> Hir.Expr): void {
            match (this) {
                | Expr as e => e.expr = e.expr.replaceAll(scope, f)
                | Var as e => e.expr = e.expr.replaceAll(scope, f)
                | Assign as e => e.expr = e.expr.replaceAll(scope, f)
                | Return as e => e.expr = e.expr.replaceAll(scope, f)
            }
        }
        
        public errors: List[Error] = List();
        
        public scanErrors(): IEnumerable[(Error * Span)] {
            def es = match (this) {
                | Expr(e) => e.scanErrors()
                | Var(_, e, _) => e.scanErrors()
                | Assign(_, e) => e.scanErrors()
                | Return(e) => e.scanErrors()
            }
            errors.Map(e => (e, span)).Concat(es)
        }
    }
    
    public class SwitchEntry: HasSpan[SwitchEntry], HasExpr {
        public mutable bodyScope: Scope.Block;
        public mutable pred: Expr;
        public mutable body: Expr;
        
        public this(pred: Expr, body: Expr) {
            this.pred = pred;
            this.body = body;
        }
        
        public replaceExpr(scope: Hir.Scope, f: Hir.Scope * Hir.Expr -> Hir.Expr): void {
            this.pred = this.pred.replaceAll(scope, f);
            this.body = this.pred.replaceAll(bodyScope, f);
        }
        
        public scanErrors(): IEnumerable[(Error * Span)] {
            pred.scanErrors().Concat(body.scanErrors())
        }
    }
    
    public class MatchEntry: HasSpan[MatchEntry], HasExpr {
        public mutable bodyScope: Scope.Block;
        public pattern: Pattern;
        public mutable body: Expr;
        
        public this(pattern: Pattern, body: Expr) {
            this.pattern = pattern;
            this.body = body;
        }
        
        public replaceExpr(scope: Hir.Scope, f: Hir.Scope * Hir.Expr -> Hir.Expr): void {
            this.body = this.body.replaceAll(bodyScope, f);
        }
        
        public scanErrors(): IEnumerable[(Error * Span)] {
            pattern.scanErrors().Concat(body.scanErrors())
        }
    }
    
    public class FnArg: HasSpan[FnArg] {
        public name: string;
        
        public this(name: string) {
            this.name = name;
        }
        
        public override ToString(): string {
            $"$name"
        }
    }
    
    public variant Expr: HasSpan[Expr], HasExpr {
        | Unit {
            public override ToString(): string {
                "Unit"
            }
        }
        | Bool {
            public value: bool;
            
            public override ToString(): string {
                $"Bool($value)"
            }
        }
        | Int {
            public value: int;
            
            public override ToString(): string {
                $"Int($value)"
            }
        }
        | Double {
            public value: double;
            
            public override ToString(): string {
                $"Double($value)"
            }
        }
        | String {
            public value: string;
            
            public override ToString(): string {
                $"String($value)"
            }
        }
        | Block {
            public mutable scope: Scope.Block;
            public mutable stmts: List[Stmt] = List();
            
            public this() { }
            
            public override ToString(): string {
                def stmtsStr = stmts.joinToString(", ");
                $"Block($stmtsStr)"
            }
        }
        | Switch {
            public entries: list[SwitchEntry];
        }
        | Match {
            public mutable scope: Scope.Block;
            public mutable expr: Expr;
            public entries: list[MatchEntry];
            
            public this(expr: Expr, entries: list[MatchEntry]) {
                this.expr = expr;
                this.entries = entries;
            }
        }
        | Fn {
            public args: list[FnArg];
            public mutable body: Expr;
            public mutable scope: Scope.Fn;
            
            public this(args: list[string]) {
                this.args = args.Map(a => FnArg(a))
            }
            
            public override ToString(): string {
                def argsStr = args.joinToString(", ");
                $"Fn($argsStr) { $body }"
            }
        }
        | Id {
            public name: string;
            mutable symbolCache: option[Symbol];
            
            public this(name: string) {
                this.name = name;
                symbolCache = None();
            }
            
            public this(symbol: Symbol) {
                this.name = symbol.name;
                symbolCache = Some(symbol);
                type.unify(symbol.type);
            }
            
            public getSymbol(scope: Hir.Scope): option[Symbol] {
                when (symbolCache.IsNone) {
                    def syms = scope.resolveVar(name, type.pruned());
                    when (syms.Count() == 1) { // シンボルが一つに絞れていれば成功
                        symbolCache = Some(syms.First());
                        _ = symbolCache.Value.type.unify(type) // Id式の型とSymbolの型は一致する
                    }
                }
                symbolCache
            }
            
            public override ToString(): string {
                $"Id($name)"
            }
        }
        | Member {
            public mutable expr: Expr;
            public name: string; // TODO Idに変更(フィールドやメソッドを一意に識別したい)
            
            public override ToString(): string {
                $"Member($expr, $name)"
            }
        }
        | StaticMember {
            public clsName: string;
            public name: string;
            
            public override ToString(): string {
                $"StaticMember(clsName, $name)"
            }
        }
        | Apply {
            public mutable fn: Expr;
            public mutable args: list[Expr];
            
            public override ToString(): string {
                def argsStr = args.joinToString(", ");
                $"Apply($fn, $argsStr)"
            }
        }
        
        public mutable typeHint: TypeHint = TypeHint.None();
        public mutable type: Type.Var = Type.Var();
        public errors: List[Error] = List();
        
        public replaceExpr(scope: Hir.Scope, f: Hir.Scope * Hir.Expr -> Hir.Expr): void {
            _ = replaceAll(scope, f);
        }
        
        // 自分自身も置換する
        public replaceAll(scope: Hir.Scope, f: Hir.Scope * Hir.Expr -> Hir.Expr): Hir.Expr {
            match (this) {
                | Unit | Bool | Int | Double | String | Id | StaticMember => {}
                | Block as e => e.stmts.ForEach(_.replaceExpr(e.scope, f))
                | Switch as e => foreach (e in e.entries) { e.replaceExpr(e.bodyScope, f) }
                | Match as e => foreach (e in e.entries) { e.replaceExpr(e.bodyScope, f) }
                | Fn as e => e.body = e.body.replaceAll(e.scope, f)
                | Member as e => e.expr = e.expr.replaceAll(scope, f)
                | Apply as e => {
                    e.fn = e.fn.replaceAll(scope, f);
                    e.args = e.args.Map(a => a.replaceAll(scope, f));
                }
            };
            f(scope, this)
        }
        
        // TODO Type.Varのエラーが重複する
        public scanErrors(): IEnumerable[(Error * Span)] {
            System.Diagnostics.Debug.WriteLine(this);
            def es = match (this) {
                | Unit | Bool | Int | Double | String => []
                | Block(_, ss) => ss.SelectMany(_.scanErrors())
                | it is Fn => it.body.scanErrors()
                | Id => []
                | Member(e, _) => e.scanErrors()
                | StaticMember => []
                | Apply(fn, args) => fn.scanErrors().Concat(args.SelectMany(_.scanErrors()))
                | Switch(es) => es.SelectMany(_.scanErrors())
                | Match(_, e, es) => e.scanErrors().Concat(es.SelectMany(_.scanErrors()))
            }
            errors.Map(e => (e, span)).Concat(es).Concat(type.scanErrors(span))
        }
    }
    
    [Record]
    public class Import {
        public path: list[string];
        public mutable value: ImportValue; // Linkで書き込み
        
        public this(path: list[string]) {
            this.path = path;
        }
        
        public dotted(): string {
            def sb = System.Text.StringBuilder();
            when (path.Length > 0)
                _ = sb.Append(path.First());
            when (path.Length > 1) {
                foreach (s in path.Tail) {
                    _ = sb.Append("." + s);
                }
            }            
            sb.ToString()
        }
        
        // TODO: errors
    }
    
    public enum AccessQualifier {
        | Public
        | Private
    }
    
    public class Method {
        public name: string;
        public fn: Expr.Fn;
        public accessQualifier: AccessQualifier;
        public mutable hidden: bool; // 自動生成の関数など（MSILでHideBySigがつく）
        
        public mutable info: option[MethodInfo];
        
        public this(name: string, fn: Expr.Fn) {
            this.name = name;
            this.fn = fn;
            this.accessQualifier = AccessQualifier.Public;
            this.hidden = false;
            
            this.info = None();
        }
        
        public isCtor(): bool {
            name == "this" // thisならConstructor
        }
        
        public scanErrors(): IEnumerable[(Error * Span)] {
            fn.scanErrors()
        }
    }
    
    public abstract class HasModules {
        public mutable modules: Hashtable[string, Atla.Lang.Hir.Module] = Hashtable();
        
        // モジュールの作成（パスの途中のモジュールも無ければ作られる）すでにあるならそれを返す
        public updateModule(modulePath: IEnumerable[string], mod: Atla.Lang.Hir.Module): void {
            if (modulePath.Count() == 0)
                throw Exception("module path must not be empty.")
            else {
                if (modulePath.Count() == 1) {
                    def moduleName = modulePath.First();
                    when (modules.ContainsKey(moduleName)) {
                        // モジュールのツリー構造を維持するために子モジュールをセットする
                        def childModules = modules[moduleName].modules;
                        mod.modules = childModules;
                    }
                    modules[moduleName] = mod;
                } else {
                    def nextParentName = modulePath.First();
                    when (!modules.ContainsKey(nextParentName)) {
                        // 途中のモジュールがないなら追加
                        modules[nextParentName] = Hir.Module(nextParentName);
                    }
                    def nextParent = modules[nextParentName];
                    nextParent.updateModule(modulePath.Skip(1), mod)
                }
            }
        }
        
        public findModule(modulePath: IEnumerable[string]): option[Hir.Module] {
            if (modulePath.Count() == 0)
                None()
            else {
                if (modulePath.Count() == 1) {
                    def moduleName = modulePath.First();
                    if (modules.ContainsKey(moduleName)) {
                        Some(modules[moduleName])
                    } else {
                        None()
                    }
                } else {
                    def nextParentName = modulePath.First();
                    if (!modules.ContainsKey(nextParentName)) {
                        None()
                    } else {
                        def nextParent = modules[nextParentName];
                        nextParent.findModule(modulePath.Skip(1))
                    }
                }
            }
        }
    }
    
    public class Module: HasModules {
        public name: string; // TODO scope.nameを返す
        public imports: List[Import];
        public mutable methods: List[Method];
        public mutable types: List[Type.Define];
        public mutable scope: Scope.Module;
        
        public this(name: string) {
            this.name = name;
            imports = List();
            methods = List();
            types = List();
        }
        
        public import(path: list[string]): void {
            imports.Add(Import(path))
            // TODO edit scope
        }
        
        public errors: List[Error] = List();
        
        public scanErrors(): IEnumerable[(Error * Span)] {
            errors.Map(e => (e, Span.zero()))
                .Concat(methods.SelectMany(_.scanErrors()))
                .Concat(types.SelectMany(t => t.scanMyErrors(Span.zero()))) // scanErrorsではないので注意 (型定義は外部から参照されるので無限ループ防止)
        }
    }
    
    public class Assembly: HasModules {
        public name: string;
        public fileName: string;
        public mutable scope: Scope.Assembly;
        
        public this(name: string, fileName: string) {
            this.name = name;
            this.fileName = fileName;
        }
        
        public scanErrors(): IEnumerable[(Error * Span)] {
            modules.Values.SelectMany(_.scanErrors())
        }
    }
}
