using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Reflection;

using Atla.Lang;
using Atla.Lang.Mir;

namespace Atla.Lang.Hir
{
    // a.k.a SymbolTable or Environment
    public variant Scope: Atla.Lang.TypeScope {
        | Global {
            __dummy: int = 0;
            
            public this() {
                depth = 0;
                
                typeTable.Add("Unit", Type.Unit);
                typeTable.Add("Bool", Type.Bool);
                typeTable.Add("Int", Type.Int);
                typeTable.Add("Float", Type.Float);
                typeTable.Add("Double", Type.Double);
                typeTable.Add("String", Type.String);
            
                varTable.Add(Symbol("+", Atla.Lang.Type.Fn([Type.Int, Type.Int], Type.Int), SymbolKind.InlineMethod(Proc.DstArg2((dst, arg0, arg1) => [
                    Mir.Ins.TAC(dst, arg0, Mir.OpCode.Add, arg1),
                ])), depth));
                varTable.Add(Symbol("+", Atla.Lang.Type.Fn([Type.Double, Type.Double], Type.Double), SymbolKind.InlineMethod(Proc.DstArg2((dst, arg0, arg1) => [
                    Mir.Ins.TAC(dst, arg0, Mir.OpCode.Add, arg1),
                ])), depth));
                varTable.Add(Symbol("-", Atla.Lang.Type.Fn([Type.Int, Type.Int], Type.Int), SymbolKind.InlineMethod(Proc.DstArg2((dst, arg0, arg1) => [
                    Mir.Ins.TAC(dst, arg0, Mir.OpCode.Sub, arg1),
                ])), depth));
                varTable.Add(Symbol("-", Atla.Lang.Type.Fn([Type.Double, Type.Double], Type.Double), SymbolKind.InlineMethod(Proc.DstArg2((dst, arg0, arg1) => [
                    Mir.Ins.TAC(dst, arg0, Mir.OpCode.Sub, arg1),
                ])), depth));
                varTable.Add(Symbol("||", Atla.Lang.Type.Fn([Type.Bool, Type.Bool], Type.Bool), SymbolKind.InlineMethod(Proc.DstArg2((dst, arg0, arg1) => [
                    Mir.Ins.TAC(dst, arg0, Mir.OpCode.Or, arg1),
                ])), depth));
                varTable.Add(Symbol("==", Atla.Lang.Type.Fn([Type.Int, Type.Int], Type.Bool), SymbolKind.InlineMethod(Proc.DstArg2((dst, arg0, arg1) => [
                    Mir.Ins.TAC(dst, arg0, Mir.OpCode.Eq, arg1),
                ])), depth));
            }
        }
        | Assembly {
            __dummy: int = 0;
            
            public parent: Scope.Global;
            public name: string;
            
            public this(parent: Scope.Global, name: string) {
                this.parent = parent;
                this.depth = parent.depth + 1;
                this.name = name;
            }
        }
        | Module {
            __dummy: int = 0;
            
            public parent: Scope;
            public name: string;
            
            public this(parent: Scope, name: string) {
                this.parent = parent;
                this.depth = parent.depth + 1;
                this.name = name;
            }
        }
        | Type {
            public parent: Scope;
            public name: string;
        }
        | Fn { // TODO この時点でFrameにしてもいいかも
            public parent: Scope;
            // TODO name あってもいいかも
            public retType: Atla.Lang.Type;
            
            public this(parent: Scope) {
                this.parent = parent;
                this.depth = parent.depth + 1;
                retType = Atla.Lang.Type.Var();
            }
            
            public resolveArgNth(n: int): option[Symbol] {
                def args = varTable.Filter(_.isArg()).NToList();
                if (0 <= n && n < args.Length)
                    Some(args.Nth(n))
                else
                    None()
            }
            
            public declareArg(id: string, type: Atla.Lang.Type): void {
                varTable.Add(Symbol(id, type, SymbolKind.Arg(), depth));
            }
        }
        | Block {
            public parent: Scope;
            public name: option[string];
            public retType: Atla.Lang.Type;
            
            public mutable endLabel: Mir.Label;
            public mutable retSymbol: Symbol;
            
            public this(parent: Scope) {
                this.parent = parent;
                this.depth = parent.depth + 1;
                this.name = None();
                retType = Atla.Lang.Type.Var();
                endLabel = Mir.Label();
            }
            
            public this(parent: Scope, name: option[string]) {
                this.parent = parent;
                this.depth = parent.depth + 1;
                this.name = name;
                retType = Atla.Lang.Type.Var();
                endLabel = Mir.Label();
            } 
        }
        
        public varTable: List[Symbol] = List();
        typeTable: Hashtable[string, Atla.Lang.Type] = Hashtable();
        implTable: Hashtable[string, Impl] = Hashtable();
        public mutable depth: int;
        
        public getParent(): option[Scope] {
            match (this) {
                | Global => None()
                | it is Module => Some(it.parent)
                | it is Type => Some(it.parent)
                | it is Fn => Some(it.parent)
                | it is Block => Some(it.parent)
            }
        }
        
        public getName(): option[string] {
            match (this) {
                | Global => None()
                | it is Module => Some(it.name)
                | it is Type => Some(it.name)
                | Fn => None()
                | it is Block => it.name
            }
        }
        
        public getRetType(): option[Atla.Lang.Type] {
            match (this) {
                | Global => None()
                | Module => None()
                | it is Type => None()
                | it is Fn => Some(it.retType)
                | it is Block => Some(it.retType)
            }
        }
        
        public path(): string {
            def p = getParent().Map(_.path()).WithDefault("");
            
            match (getName()) {
                | Some(s) => if (p.Length > 0) $"$p::$s" else s
                | None => p
            }
        }
        
        public fullName(name: string): string {
            def p = path();
            if (p.Length > 0) $"$p::$name" else name
        }
        
        // TODO 経路圧縮
        public globalScope(): Scope {
            match (getParent()) {
                | Some(p) => p.globalScope()
                | _ => this
            }
        }
        
        public declareType(id: string, type: Atla.Lang.Type): void {
            // TODO 重複エラー
            typeTable.Add(id, type)
        }
        
        public resolveType(id: string): Atla.Lang.Type {
            if (typeTable.ContainsKey(id)) {
                typeTable.Get(id).Value
            } else {
                match (getParent()) {
                    | Some(p) => p.resolveType(id)
                    | _ => Atla.Lang.Type.Error(TypeError.Undefined(id))
                }
            }
        }
        
        public declareLocal(id: string, type: Atla.Lang.Type): Symbol {
            def sym = Symbol(id, type, SymbolKind.Local(), depth);
            varTable.Add(sym);
            sym
        }
        
        // 名前解決での優先順で返す（内側のスコープを優先）
        public resolveVar(id: string, type: Atla.Lang.Type): IEnumerable[Symbol] {
            def syms = varTable.Filter(s => s.name == id && type.canUnify(s.type)) : list[Symbol];
            
            if (syms.Length == 0) {
                match (getParent()) {
                    | Some(s) => s.resolveVar(id, type)
                    | _ => []
                }
            } else {
                syms // overloadなどで複数にマッチしてもいい
            }
        }
    }
}
