using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Reflection;

using Atla.Lang;
using Atla.Lang.Parse;
using Atla.Lang.Ast;

namespace Atla.Lang.Parser
{
    public class TokenInput: Input[Token] {
        data: list[Token];
        index: int;
        
        public this(data: list[Token], index: int) {
            this.data = data.Filter(t => !(t is Token.Comment)); // remove comments
            this.index = index;
        }
        
        public get(): option[Token] {
            if (0 <= index && index < data.Length)
                Some(data.ElementAt(index))
            else
                None()
        }
        
        public position(): Span {
            match (get()) {
                | Some(t) => t.span
                | _ when index >= data.Length && data.Length > 0 => {
                    def p = data.Last().span.hi;
                    Span(p, p.right()) // end of data
                }
                | _ => Span.zero()
            }
        }
        
        public next(): Input[Token] {
            TokenInput(data, index + 1)
        }
    }
    
    public class BlockInput: Input[Token] {
        public parent: Input[Token];
        openBy: Token;
        openAt: Position;
        
        public this(parent: Input[Token], openBy: Token) {
            this.parent = parent;
            this.openBy = openBy;
            
            // 行頭をブロック開始位置とする
            def line = openBy.span.lo.line;
            def col = openBy.span.lo.lines.Nth(line).TakeWhile(_ == ' ').Count();
            def index = openBy.span.lo.index - (openBy.span.lo.col - col);
            this.openAt = Position(openBy.span.lo.lines, line, col, index);
        }
        
        public get(): option[Token] {
            match (parent.get()) {
                | Some(t) => {
                    if (t.span.lo.line == openAt.line && t.span.lo.col <= openAt.col) // openしたTokenも含める
                        Some(t)
                    else if (t.span.lo.col <= openAt.col)
                        None()
                    else
                        Some(t)
                }
                | _ => None()
            }
        }
        
        public position(): Span {
            parent.position() :> Span
        }
        
        public next(): Input[Token] {
            BlockInput(parent.next(), openBy)
        }
    }
    
    public variant OnceResult[I, A] {
        | Ok { value: A }
        | Ng { failure: Result[I, A] }
    }
    
    public class OffsideParserEnv: ParserEnv[Token] {
        
    }
    
    public class Parser: OffsideParserEnv
    {
        public typeHint: ParserRef[Token, TypeHint] = ParserRef();
        public pattern: ParserRef[Token, Pattern] = ParserRef();
        public fn: ParserRef[Token, Ast.Fn] = ParserRef();
        public expr: ParserRef[Token, Expr] = ParserRef();
        public stmt: ParserRef[Token, Stmt] = ParserRef();
        public decl: ParserRef[Token, Decl] = ParserRef();
        public mod: Parser[Token, Ast.Module];
        
        public this() {
            def tid: Parser[Token, Token.Id] = satisfy("id", _ is Token.Id).map(_ :> Token.Id);
            def infixOp(precedence: int): Parser[Token, Token.InfixOp] {
                satisfy("infixOp", t => t is Token.InfixOp && (t :> Token.InfixOp).precedence == precedence).map(_ :> Token.InfixOp)
            }
            
            // TypeHint
            {
                def typeHintFactor: Parser[Token, TypeHint] = tid.map(a => TypeHint.Name(a.value).exSpan(a));
                def typeHintParen: Parser[Token, TypeHint] = delim("(")._and(typeHint.optional().map(mt => {
                    match (mt) {
                        | Some(t) => t
                        | _ => TypeHint.Unit()
                    }
                }))._and(delim(")")).map((ab, c) => ab[1].exSpan([ab[0], c]));
                def typeHintTerm = typeHintParen._or(typeHintFactor);
                
                def typeHintGenerics: Parser[Token, TypeHint] = typeHintTerm.many1().map(ts => {
                    if (ts.Length == 1)
                        ts.Head
                    else
                        TypeHint.Generics(ts.Head, ts.Tail)
                });
                
                def typeHintFn: Parser[Token, TypeHint] = typeHintGenerics.sepBy1(delim("->")).map(ts => {
                    if (ts.Length > 1)
                        TypeHint.Fn(ts.Take(ts.Length - 1).NToList(), ts.Last())
                    else
                        ts.Head
                });
                
                typeHint.impl = typeHintFn;
            }
            
            // Pattern
            {
                def int = satisfy("int", _ is Token.Int).map(t => Pattern.Int((t :> Token.Int).value).exSpan([t]));
                def any = satisfy("any", t => t is Token.Id && (t :> Token.Id).value == "_").map(t => Pattern.Any().exSpan([t])); // delim("_")では無いので注意
                pattern.impl = int._or(any);
            }
            
            // Fn
            {
                def fnArg: Parser[Token, FnArg] = log("fn_arg", delim("(")._and(tid._andL(delim(":"))._and(typeHint).optional())._and(delim(")")).map((a_mit, b) => {
                    def (a, mit) = a_mit;
                    match (mit) {
                        | Some((i, t)) => FnArg(i.value, t).exSpan([a.span, b.span])
                        | _ => FnArg("", TypeHint.Unit())
                    }
                }));
                def fnArgs: Parser[Token, list[FnArg]] = fnArg.many();
                fn.impl = log("fn", delim("fn")._and(delim("this")._andL(delim(".")).optional()._and(tid)._and(fnArgs)._andL(delim(":"))._and(typeHint)._andL(delim("="))._and(expr).map((mt_n_rs_r, c) => {
                        def mt = mt_n_rs_r[0][0][0];
                        def n = mt_n_rs_r[0][0][1];
                        def rs = mt_n_rs_r[0][1];
                        def r = mt_n_rs_r[1];
                        if (mt.IsNone) {
                            if (rs.Length > 0)
                                Ast.Fn.Static(n.value, rs, r, c).exSpan([n.span, c.span])
                            else
                                Ast.Fn.Error("static method must have at least one argument.")
                        } else
                            Ast.Fn.Instance(n.value, rs, r, c).exSpan([n.span, c.span])
                    })._andL(eoi()).onceFn()
                ).map((a, b) => b.exSpan(a)));
            }
            
            // Expr
            {
                def id = log("expr_id", tid.map(t => Expr.Id(t.value).exSpan(t)));
                def unit = log("expr_unit", delim("(")._and(delim(")")).map((a, b) => Expr.Unit().exSpan([a, b])));
                def int = log("expr_int", satisfy("int", _ is Token.Int).map(t => Expr.Int((t :> Token.Int).value).exSpan(t)));
                def double = log("expr_double", satisfy("double", _ is Token.Double).map(t => Expr.Double((t :> Token.Double).value).exSpan(t)));
                def str = log("expr_string", satisfy("string", _ is Token.String).map(t => Expr.String((t :> Token.String).value).exSpan(t)));
                def paren = log("expr_paren", delim("(")._andR(expr)._andL(delim(")")));
                def blockExpr = log("do", delim("do")._and(
                    stmt.many1().map(ss => Expr.Block(ss).exSpan(ss)).onceExpr()
                ).map((a, b) => b.exSpan(a)));

                // If
                def ifThenEntry = log("if_then_entry", delim("|")._and(expr._andL(delim("=>"))._and(expr.onceExpr()).map((p, e) => {
                    IfEntry.Then(p, e).exSpan(e.span)
                })).map((a, b) => b.exSpan(a)));
                def ifElseEntry = log("if_else_entry", delim("|")._and(delim("else")._andL(delim("=>"))._andR(expr.onceExpr()).map(e => {
                    IfEntry.Else(e).exSpan(e.span)
                })).map((a, b) => b.exSpan(a)));
                def ifExpr: Parser[Token, Expr] = log("if", delim("if")._and(
                    ifThenEntry._or(ifElseEntry).many1().map(cs => {
                        Expr.If(cs).exSpan(cs.Map(_.span))
                    }).onceExpr()
                ).map((a, b) => b.exSpan(a)).consumeBlock());
                
                // Match
                def matchEntry = log("match_entry", delim("|")._and(pattern._andL(delim("=>"))._and(expr).map((p, e) => {
                    MatchEntry.Case(p, e).exSpan([p.span, e.span])
                })).map((a, b) => b.exSpan(a)));
                def matchExpr: Parser[Token, Expr] = log("match", delim("match")._and(
                    expr._and(matchEntry.many1()).map((e, cs) => {
                        Expr.Match(e, cs).exSpan(e.span :: cs.Map(_.span))
                    }).onceExpr()
                ).map((a, b) => b.exSpan(a)).consumeBlock());
                
                def factor = str._or(ifExpr)._or(matchExpr)._or(blockExpr)._or(id)._or(int)._or(double)._or(unit)._or(paren);
            
                // メンバーアクセス
                def member = log("expr_member", factor._andL(delim("."))._and(tid).map((a, b) => Expr.Member(a, b.value).exSpan([a.span, b.span])));
                def staticMember = log("expr_static", tid._andL(delim("::"))._and(tid).map((a, b) => Expr.StaticMember(a.value, b.value).exSpan([a.span, b.span])));
                def term1 = staticMember._or(member)._or(factor);
                
                // 呼び出し式
                def term2 = log("expr_apply", term1.many1().map(es => {
                    if (es.Length == 1)
                        es.First()
                    else
                        Expr.Apply(es.First(), es.Tail).exSpan(es)
                }));
                
                // 二項演算
                def binop = Enumerable.Range(2, 5).Reverse().Fold(term2, (i, acc) => {
                    def op = infixOp(i);
                    acc._and(op._and(acc).optional()).map((a, m_op_b) => {
                        match (m_op_b) {
                            | Some(op_b) => {
                                def op = op_b[0];
                                def b = op_b[1];
                                def fn = Expr.Id(op.value).addSpan(op.span);
                                Expr.Apply(fn, [a, b]).addSpan([a.span, b.span])
                            }
                            | _ => a
                        }
                    })
                });
                
                expr.impl = binop.inlineBlock();
            }
            
            // Stmt
            {
                def valStmt = log("stmt_val", delim("val")._and(tid._andL(delim("="))._and(expr).map((id, e) => {
                    Stmt.Val(id.value, e).exSpan([id.span, e.span])
                }).onceStmt()).map((a, b) => b.exSpan(a)));
                
                def varStmt = log("stmt_var", delim("var")._and(tid._andL(delim("="))._and(expr).map((id, e) => {
                    Stmt.Var(id.value, e).exSpan([id.span, e.span])
                }).onceStmt()).map((a, b) => b.exSpan(a)));
                
                def assignStmt = log("stmt_assign", tid._andL(delim("="))._and(expr).map((id, e) => {
                    Stmt.Assign(id.value, e).exSpan([id.span, e.span])
                }));
                
                def exprStmt = log("stmt_expr", expr.map(e => Stmt.Expr(e).exSpan(e)));
                
                def retStmt = log("stmt_return", delim("return")._and(expr.optional()).map((a, b) => {
                    def ret = Stmt.Ret(b.WithDefault(Expr.Unit())).exSpan(a);
                    if (b.IsSome) ret.exSpan(b.Value) else ret
                }));
                
                def fnStmt: Parser[Token, Stmt] = fn.map(f => Stmt.Fn(f).exSpan(f));
                
                def forStmt = log("stmt_for", delim("for")._and(tid._andL(delim("in"))._and(expr)._andL(delim("=>"))._and(stmt.many()).map((id_e, stmts) => {
                    Stmt.For(id_e[0].value, id_e[1], stmts).exSpan(id_e[0].span :: stmts.Map(_.span))
                }).onceStmt()).map((a, b) => b.exSpan(a)));
                
                stmt.impl = valStmt._or(varStmt)._or(retStmt)._or(fnStmt)._or(forStmt)._or(assignStmt)._or(exprStmt).onceStmt().consumeBlock();
            }
            
            // Decls
            {
                // ModuleFn
                def modFnDecl = fn.map(f => match (f) {
                    | Fn.Static as f => Decl.StaticFn(f).exSpan(f)
                    | Fn.Instance as f => Decl.Error("Cannot declare Instance method in module.")
                    | Fn.Error(s) => Decl.Error(s)
                });
            
                // Type
                def typeValField: Parser[Token, TypeItem] = log("type_val", delim("val")._and(tid._andL(delim(":"))._and(typeHint)._and(delim("=")._andR(expr).optional()).map((s_t, e) => {
                    def (s, t) = s_t: Token.Id * TypeHint;
                    TypeItem.Field(s.value, t, false, e)
                }).onceTypeItem()).map((a, b) => b.exSpan(a)));
                def typeVarField: Parser[Token, TypeItem] = log("type_var", delim("var")._and(tid._andL(delim(":"))._and(typeHint)._and(delim("=")._andR(expr).optional()).map((s_t, e) => {
                    def (s, t) = s_t: Token.Id * TypeHint;
                    TypeItem.Field(s.value, t, true, e)
                }).onceTypeItem()).map((a, b) => b.exSpan(a)));
                def typeMethod: Parser[Token, TypeItem] = log("type_method", fn.map(d => TypeItem.Method(d).exSpan(d)));
                def typeItem = typeValField._or(typeVarField)._or(typeMethod);
                def typeDecl = log("type", delim("type")._and(
                    tid._andL(delim("="))._and(typeItem.many()).map((a, items) => {
                        if (items.Length > 0)
                            Decl.Type(a.value, items).exSpan(a.span :: items.Map(i => i.span).LastN(1))
                        else
                            Decl.Type(a.value, items).exSpan(a.span)
                    }).onceDecl()
                ).map((a, b) => b.exSpan(a)));
            
                // Import
                def importDecl = log("import", delim("import")._and(
                    tid._and(delim("::")._andR(tid).many()).map((x, xs) => {
                        def path = x.value :: xs.Map(t => t.value);
                        if (xs.Length > 1)
                            Decl.Import(path).exSpan((x :: xs.LastN(1)).Map(t => t))
                        else
                            Decl.Import(path).exSpan(x)
                    })._andL(eoi()).onceDecl()
                ).map((a, b) => b.exSpan(a)));
            
                decl.impl = importDecl._or(typeDecl)._or(modFnDecl).consumeBlock();
            }
            
            mod = decl.many().map(ds => {
                Ast.Module(ds).exSpan(ds)
            })._andL(eoi());
        }
        
        delim(s: string): Parser[Token, Token] {
            satisfy($"delimiter $s", t => {
                match (t) {
                    | Token.Delim(ts) when (s == ts) => true
                    | _ => false
                }
            })
        }
    }
    
    public module ParserExtensions {
        public onceExpr(this p: Parser[Token, Expr]): Parser[Token, Expr] {
            p.skipFail(ret => {
                def failure = (ret :> ParseResult[Token, Expr].Failure).error : ParseFailure[Token]; // always it is failure
                def ret = Expr.Error(failure.reason);
                ret.span = failure.failAt.position() :> Span;
                ret
            })
        }
        
        public onceStmt(this p: Parser[Token, Stmt]): Parser[Token, Stmt] {
            p.skipFail(ret => {
                def failure = (ret :> ParseResult[Token, Stmt].Failure).error : ParseFailure[Token]; // always it is failure
                def ret = Stmt.Error(failure.reason);
                ret.span = failure.failAt.position() :> Span;
                ret
            })
        }
        
        public onceDecl(this p: Parser[Token, Decl]): Parser[Token, Decl] {
            p.skipFail(ret => {
                def failure = (ret :> ParseResult[Token, Decl].Failure).error : ParseFailure[Token]; // ret is always failure
                def ret = Decl.Error(failure.reason);
                ret.span = failure.failAt.position() :> Span;
                ret
            })
        }
        
        public onceTypeItem(this p: Parser[Token, TypeItem]): Parser[Token, TypeItem] {
            p.skipFail(ret => {
                def failure = (ret :> ParseResult[Token, TypeItem].Failure).error : ParseFailure[Token]; // ret is always failure
                def ret = TypeItem.Error(failure.reason);
                ret.span = failure.failAt.position() :> Span;
                ret
            })
        }

        public onceFn(this p: Parser[Token, Fn]): Parser[Token, Fn] {
            p.skipFail(ret => {
                def failure = (ret :> ParseResult[Token, Fn].Failure).error : ParseFailure[Token]; // always it is failure
                def ret = Fn.Error(failure.reason);
                ret.span = failure.failAt.position() :> Span;
                ret
            })
        }
        
        public exSpan[T](this ast: T, span: Span): T where T: HasSpan[T] {
            ast.addSpan(span)
        }
        
        public exSpan[T](this ast: T, token: Token): T where T: HasSpan[T] {
            ast.exSpan(token.span)
        }
        
        public exSpan[T, S](this ast: T, s: S): T where T: HasSpan[T] where S: HasSpan[S] {
            ast.exSpan(s.span)
        }
        
        public exSpan[T](this ast: T, spans: IEnumerable[Span]): T where T: HasSpan[T] {
            if (spans.IsEmpty())
                ast
            else
                ast.addSpan(spans.Skip(1).FoldRight(spans.First(), (a, b) => a + b))
        }
        
        public exSpan[T](this ast: T, tokens: IEnumerable[Token]): T where T: HasSpan[T] {
            ast.exSpan(tokens.Map(t => t.span))
        }
        
        public exSpan[T, S](this ast: T, ss: IEnumerable[S]): T where T: HasSpan[T] where S: HasSpan[S] {
            ast.exSpan(ss.Map(t => t.span))
        }
        
        public consumeBlock[A](this p: Parser[Token, A]): Parser[Token, A] {
            AnnoParser(input => {
                match (input.get()) {
                    | Some(openBy) => p.parse(BlockInput(input, openBy)).bind(success => {
                        def blockNext = success.next :> BlockInput;
                        match (blockNext.get()) {
                            | Some => ParseResult.Failure(ParseFailure("A block parser did not reach EOI.", blockNext))
                            | _ => ParseResult.Success(ParseSuccess(success.result, blockNext.parent)) // unwrap
                        }
                    })
                    | _ => ParseResult.Failure(ParseFailure("reach EOI", input))
                }
            })
        }
        
        public inlineBlock[A](this p: Parser[Token, A]): Parser[Token, A] {
            AnnoParser(input => {
                match (input.get()) {
                    | Some(openBy) => p.parse(BlockInput(input, openBy)).bind(success => {
                        ParseResult.Success(ParseSuccess(success.result, (success.next :> BlockInput).parent)) // unwrap EOIまで読み込まなくてもいい
                    })
                    | _ => ParseResult.Failure(ParseFailure("reach EOI", input))
                }
            })
        }
    }
}