using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;
using Atla;

namespace Atla.Lang.Ast
{
    // 脱糖
    // タイプヒントは明示された分だけ
    // スコープは作るけど操作は無し
    class Trans
    {
        public trans(typeHint: Ast.TypeHint): Hir.TypeHint {
            def hint = match (typeHint) {
                | Ast.TypeHint.Unit => Hir.TypeHint.Name(Hir.Scope.unitTypeName)
                | Ast.TypeHint.Fn(args, ret) => Hir.TypeHint.Fn(args.Map(trans), trans(ret))
                | Ast.TypeHint.Id(value) => Hir.TypeHint.Name(value)
                | Ast.TypeHint.Generics(_base, _params) => Hir.TypeHint.Generics(trans(_base), _params.Map(trans))
            };
            
            hint.addSpan(typeHint.span)
        }
        
        public trans(typeHint: option[Ast.TypeHint]): Hir.TypeHint {
            if (typeHint.IsNone) Hir.TypeHint.Any() else trans(typeHint.Value)
                
        }
        
        public trans(scope: Hir.Scope, ast: Ast.Stmt): Hir.Stmt {
            def stmt: Hir.Stmt = match (ast) {
                | Ast.Stmt.Expr(expr) => Hir.Stmt.Expr(trans(scope, expr))
                | Ast.Stmt.Let(name, expr) => {
                    def rhs = trans(scope, expr);
                    Hir.Stmt.Let(name, rhs)
                }
                | Ast.Stmt.Assign(name, expr) => {
                    def rhs = trans(scope, expr);
                    Hir.Stmt.Assign(name, rhs)
                }
                | Ast.Stmt.Ret(expr) => Hir.Stmt.Return(trans(scope, expr))
            };
            stmt.addSpan(ast.span)
        }
        
        public trans(scope: Hir.Scope, ast: Ast.Expr): Hir.Expr {
            def expr: Hir.Expr = match (ast) {
                | Ast.Expr.Unit => Hir.Expr.Unit()
                | Ast.Expr.Int(value) => Hir.Expr.Int(value)
                | Ast.Expr.String(value) => Hir.Expr.String(value)
                | Ast.Expr.Block(stmts) => {
                    def block = Hir.Expr.Block(scope);
                    block.stmts = stmts.Map(s => trans(block.scope, s)).ToList();
                    block
                }
                | Ast.Expr.Fn(args, body) => {
                    def fn = Hir.Expr.Fn(scope, args.Map(_.name));
                    foreach ((a, h) in args.Zip(fn.args)) {
                        h.typeHint = trans(a.typeHint);
                    }
                    fn.body = trans(fn.scope, body);
                    fn
                }
                | Ast.Expr.Id(value) => Hir.Expr.Id(value)
                | Ast.Expr.Member(expr, member) => Hir.Expr.Member(trans(scope, expr), member)
                | Ast.Expr.Apply(fn, args) => Hir.Expr.Apply(trans(scope, fn), args.Map(e => trans(scope, e)))
            }
            expr.typeHint = trans(ast.typeHint);
            expr.addSpan(ast.span)
        }
        
        public declare(mod: Hir.Module, ast: Ast.Decl): void {
            match (ast) {
                | Ast.Decl.Import(path) => mod.import(path)
                | Ast.Decl.Fn(name, args, ret, body) => {
                    def fnType = Hir.TypeHint.Fn(args.Map(a => trans(a.typeHint)), trans(ret));
                    _ = fnType.addSpan(args.Map(_.span) + [ret.span]);
                    
                    def fn = Hir.Expr.Fn(mod.scope, args.Map(_.name));
                    fn.typeHint = fnType;
                    
                    mod.declareFn(name, fn);
                    fn.body = trans(fn.scope, body); // 関数宣言でスコープを作ってから本体を定義
                }
            }
        }
        
        public trans(ast: Ast.Module, name: string): Hir.Module {
            def mod = Hir.Module(name);
            foreach (decl in ast.decls) {
                declare(mod, decl);
            }
            mod
        }
    }
}
