using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Atla.Lang.Parse;

namespace Atla.Lang.Parse
{
    public variant Result[I, A]
    {
        | Success { result: A; next: Input[I] }
        | Failure { reason: string; failAt: Input[I] }
        
        public static expectedButGot(expected: string, got: I, input: Input[I]): Failure {
            Failure($"expected $expected, but got $got.", input)
        }
        
        public static eof(expected: string, input: Input[I]): Failure {
            Failure($"expected $expected, but reached EOF.", input)
        }
        
        public override ToString(): string {
            match (this) {
                | Success(result, next) => $"Success { result: $result, next: $next }"
                | Failure(reason, failAt) => $"Failure { reason: $reason, failAt: $failAt }"
            }
        }
    }
}
