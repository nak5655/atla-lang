using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;
using Atla.Lang.Parse;

namespace Atla.Lang.Parse
{
    public class ParserEnv[I] {
        logStack: System.Collections.Generic.Stack[string] = System.Collections.Generic.Stack();
        
        public satisfy(name: string, pred: I -> bool): Parser[I, I] {
            AnnoParser(input => {
                match (input.get()) {
                    | Some(got) => {
                        if (pred(got)) {
                            ParseResult.Success(ParseSuccess(got, input.next()))
                        } else {
                            ParseResult.Failure(ParseFailure.expectedButGot(name, got, input))
                        }
                    }
                    | _ => ParseResult.Failure(ParseFailure.eof(name, input))
                }
            })
        }
        
        public phrase[A](name: string, seq: list[A], eq: I * A -> bool): Parser[I, list[I]] {
            AnnoParser(input => {
                mutable current = input;
                def ret: List[I] = List();
                mutable i = 0;
                while (i < seq.Count()) {
                    match (current.get()) {
                        | Some(got) => {
                            when (!eq(got, seq.Nth(i))) {
                                return ParseResult.Failure(ParseFailure.expectedButGot(name, got, current))
                            }
                            ret.Add(got);
                            current = current.next();
                            i += 1;
                        }
                        | _ => return ParseResult.Failure(ParseFailure.eof(name, input))
                    }
                }
                
                ParseResult.Success(ParseSuccess(ret.AsList(), current))
            })
        }
        
        public pure[A](value: A): Parser[I, A] {
            AnnoParser(input => {
                ParseResult.Success(ParseSuccess(value, input))
            })
        }
        
        public fail[A](reason: string): Parser[I, A] {
            AnnoParser(input => {
                ParseResult.Failure(ParseFailure(reason, input))
            })
        }
        
        // use object instead of unit (or void)
        public eoi(): Parser[I, object] {
            def t = ();
            AnnoParser(input => {
                match (input.get()) {
                    | Some(c) => ParseResult.Failure(ParseFailure.expectedButGot("EOI", c, input))
                    | _ => ParseResult.Success(ParseSuccess(null, input))
                }
            }): Parser[I, object]
        }
        
        public _ref[A](): ParserRef[I, A] {
            ParserRef()
        }
        
        public log[A](name: string, p: Parser[I, A]): Parser[I, A] {
            AnnoParser(input => {
                _ = logStack.Push(name);
                
                def res = p.parse(input);
                match (res) {
                    | ParseResult.Success => System.Diagnostics.Debug.WriteLine($"ParserLog: $(logStack.Reverse().joinToString(\">\")) OK")
                    | ParseResult.Failure(ParseFailure(reason, _)) => System.Diagnostics.Debug.WriteLine($"ParserLog: $(logStack.Reverse().joinToString(\">\")) NG $reason")
                }
                
                when (logStack.Pop() != name)
                    throw Exception("log error");
                
                res
            })
        }
    }
    
    public class ParserRef[I, A]: Parser[I, A] {
        public mutable impl: Parser[I, A];
        
        public override apply(input: Input[I]): ParseResult[I, A] {
            impl.apply(input)
        }
    }
}