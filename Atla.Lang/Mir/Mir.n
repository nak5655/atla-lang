using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Threading;

using System.Reflection;
using System.Reflection.Emit;

// ネストした関数やスコープを除去済み
// 関数のローカル変数のスタック位置が確定 -> 変数名を削除して添え字でアクセスできる
// 関数や型はMethodInfoとSystem.Typeで参照する
// K正規化済み
// TODO 同じMarkLabelが複数ないかチェック
namespace Atla.Lang.Mir
{
    public variant Imm {
        | Bool { value: bool }
        | Int { value: int }
        | Double { value: double }
        | String { value: string }
        
        public override ToString(): string {
            match (this) {
                | Bool(v) => $"Bool($v)"
                | Int(v) => $"Int($v)"
                | Double(v) => $"Double($v)"
                | String(v) => $"String($v)"
            }
        }
    }
    
    public variant Value {
        | Imm { value: Mir.Imm }
        | Sym { value: Symbol }
        | Field { inst: Symbol; field: FieldInfo }
        
        public override ToString(): string {
            match (this) {
                | Imm(v) => $"Imm($v)"
                | Sym(v) => $"Sym($v)"
                | Field(sym, fi) => $"Field($sym, $fi)"
            }
        }
    }
    
    public enum OpCode {
        | Add // 算術演算
        | Sub
        | Mul
        | Div
        | Or // 論理演算
        | And
        | Eq // 同値
    }
    
    public class Label {
            public override ToString(): string {
                $"Label($(GetHashCode()))"
            }
    }
    
    // ここではまだスタックマシンじゃないので注意 (K正規化まではしてる)
    public variant Ins {
        | Assign {
            name: Symbol;
            value: Value;
            
            public override ToString(): string {
                $"$name = $value"
            }
        }
        | AssignField {
            inst: Symbol;
            field: FieldInfo;
            value: Value;
            
            public override ToString(): string {
                $"$inst.field = $value"
            }
        }
        | TAC { // three addess code
            dest: Symbol; lhs: Value; op: OpCode; rhs: Value;
        
            public override ToString(): string {
                $"$dest = $lhs $op $rhs"
            }
        }
        | Call {
            method: MethodInfo;
            args: list[Value];
            
            public override ToString(): string {
                $"$method($(args.joinToString(\", \")))"
            }
        }
        | CallStatic {
            method: MethodInfo;
            args: list[Value];
            
            public override ToString(): string {
                $"$method($(args.joinToString(\", \")))"
            }
        }
        | CallAssign {
            dst: Symbol;
            method: MethodInfo;
            args: list[Value];
            
            public override ToString(): string {
                $"$dst = $method($(args.joinToString(\", \")))"
            }
        }
        | Construct { // Constructor
            dst: Symbol;
            ctor: ConstructorInfo;
            args: list[Value];
            
            public override ToString(): string {
                $"$dst = $ctor($(args.joinToString(\", \")))"
            }
        }
        | Ret
        | RetValue {
            value: Value;
            
            public override ToString(): string {
                $"return $value"
            }
        }
        | Jump {
            label: Label;
            
            public override ToString(): string {
                $"Jump $label"
            }
        }
        | JumpTrue {
            value: Value;
            label: Label;
            
            public override ToString(): string {
                $"JumpTrue $value $label"
            }
        }
        | MarkLabel {
            label: Label;
            
            public override ToString(): string {
                $"MarkLabel $label"
            }
        }
    }
    
    public class Field {
        public name: string;
        public builder: FieldBuilder;
        
        public this(ty: Type, name: string, type: System.Type) {
            this.name = name;
            this.builder = ty.builder.DefineField(name, type, FieldAttributes.Public);
        }
    }
    
    public class Constructor {
        public builder: ConstructorBuilder;
        
        public this(builder: ConstructorBuilder) {
            this.builder = builder;
        }
    }
    
    public class Method {
        public name: string;
        public builder: MethodBuilder; // subclass of MethodInfo
        public mutable body: list[Ins];
        
        public frame: Frame = Frame();
        
        public this(ty: Type, name: string, args: list[System.Type], ret: System.Type) {
            this.name = name;
            builder = ty.builder.DefineMethod(name, MethodAttributes.Public | MethodAttributes.Static, ret, args.ToArray());
        }
        
        public this(mod: Module, name: string, args: list[System.Type], ret: System.Type) {
            this.name = name;
            builder = mod.builder.DefineGlobalMethod(name, MethodAttributes.Public | MethodAttributes.Static, ret, args.ToArray());
        }
    }
    
    public interface MethodContainer {
        defineMethod(name: string, args: list[System.Type], ret: System.Type): Method;
    }
    
    public class Type: MethodContainer {
        public builder: TypeBuilder;
        
        public fields: List[Field];
        public ctors: List[Constructor];
        public methods: List[Method];
        
        public this(mod: Module, name: string) {
            this.builder = mod.builder.DefineType(name, TypeAttributes.Public, typeof(System.Object));
            this.fields = List();
            this.ctors = List();
            this.methods = List();
        }
        
        public defineField(name: string, ret: System.Type): Field {
            def field = Field(this, name, ret);
            fields.Add(field);
            field
        }
        
        public defineConstructor(args: IEnumerable[System.Type]): Constructor {
            def ctorBuilder = builder.DefineConstructor(MethodAttributes.Public, CallingConventions.Standard, args.ToArray());
            def ctor = Constructor(ctorBuilder);
            ctors.Add(ctor);
            ctor
        }
        
        public defineMethod(name: string, args: list[System.Type], ret: System.Type): Method {
            def method = Method(this, name, args, ret);
            methods.Add(method);
            method
        }
    }
    
    public class Module: MethodContainer {
        public builder: ModuleBuilder;
        
        public types: List[Type];
        public methods: List[Method];
        
        public this(asm: Assembly, name: string) {
            this.builder = asm.builder.DefineDynamicModule(name, asm.fileName);
            this.types = List();
            this.methods = List();
        }
        
        public defineMethod(name: string, args: list[System.Type], ret: System.Type): Method {
            def method = Method(this, name, args, ret);
            methods.Add(method);
            method
        }
    }
    
    public class Assembly {
        public name: AssemblyName;
        public fileName: string;
        public builder: AssemblyBuilder;
        
        public modules: Hashtable[string, Module];
        
        public this(name: string, filePath: string) {
            this.name = AssemblyName(name);
            this.fileName = System.IO.Path.GetFileName(filePath);
            this.builder = Thread.GetDomain().DefineDynamicAssembly(this.name, AssemblyBuilderAccess.Save, System.IO.Path.GetDirectoryName(filePath));
            this.modules = Hashtable();
        }
    }
}
