using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Reflection;
using System.Reflection.Emit;

using Atla.Lang;

// TODO rename to MSIL ?
namespace Atla.Lang.Cir
{
    public class Label {
        mutable label: option[System.Reflection.Emit.Label];
        
        public this() {
            label = None();
        }
        
        public get(gen: ILGenerator): System.Reflection.Emit.Label {
            when (label.IsNone) {
                label = Some(gen.DefineLabel());
            }
            
            label.Value
        }
    }
    
    public variant Ins {
        | LdLoc { index: int }
        | StLoc { index: int }
        | LdArg { index: int }
        | StArg { index: int }
        | LdI32 { value: int }
        | LdF64 { value: double }
        | LdStr { str: string }
        | StFld { fieldInfo: FieldInfo }
        | LdFld { fieldInfo: FieldInfo }
        | Add
        | Sub
        | Mul
        | Div
        | Rem
        | Or
        | And
        | Eq
        | Call { method: Either[MethodInfo, ConstructorInfo] }
        | CallVirt { method: MethodInfo }
        | NewObj { ctor: ConstructorInfo }
        | Ret
        | BeginExceptionBlock
        | BeginFinallyBlock
        | EndExceptionBlock
        | Nop
        | MarkLabel { label: Label }
        | BrS { label: Label } // jump
        | BrTrueS { label: Label }
        | BrFalseS { label: Label }
    }
    
    [Record]
    public class Method {
        public builder: MethodBuilder; // subtype of MethodInfo
        public frame: Frame;
        public body: list[Ins];
    }
    
    [Record]
    public class Constructor {
        public builder: ConstructorBuilder; // subtype of ConstructorInfo
        public frame: Frame;
        public body: list[Ins];
    }
    
    [Record]
    public class Type {
        public builder: TypeBuilder;
        public ctors: list[Constructor];
        public methods: list[Method];
    }
    
    [Record]
    public class Module
    {
        public builder: ModuleBuilder;
        public mutable types: list[Type];
        public mutable methods: list[Method];
    }
    
    [Record]
    public class Assembly
    {
        public fileName: string;
        public builder: AssemblyBuilder;
        public mutable modules: list[Module];
    }
}
