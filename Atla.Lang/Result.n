using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Imperative;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Atla.Lang
{
    public variant Result[A, E] {
        | Success { result: A }
        | Failure { error: E }
        
        public getResult(): option[A] {
            match (this) {
                | Success(r) => Some(r)
                | _ => None()
            }
        }
        
        public isFailure(): bool {
            match (this) {
                | Success => false
                | Failure => true
            }
        }
        
        public getError(): option[E] {
            match (this) {
                | Success => None()
                | Failure(s) => Some(s)
            }
        }
        
        public map[B](f: A -> B): Result[B, E] {
            match (this) {
                | Success(r) => Result.Success(f(r))
                | Failure(e) => Result.Failure(e)
            }
        }
        
        public bind[B](f: A -> Result[B, E]): Result[B, E] {
            match (this) {
                | Success(r) => f(r)
                | Failure(e) => Result.Failure(e)
            }
        }
    }
    
    public module ResultExtensions {
        public seq[A, E](this vs: IEnumerable[Result[A, E]]): Result[IEnumerable[A], E] {
            def res = List();
            foreach (v in vs) {
                match (v) {
                    | Success(r) => res.Add(r)
                    | Failure(e) => return Result.Failure(e)
                }
            }
            
            Result.Success(res)
        }
    }
}
